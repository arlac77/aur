# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>

pkgname=gitea
pkgver=1.12.3
pkgrel=1
pkgdesc="Painless self-hosted Git service. Community managed fork of Gogs."
arch=(any)
url="https://gitea.io"
license=(MIT)
depends=(git)
makedepends=(go npm)
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'redis: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/gitea/app.ini')
_tag=77af0a23c473eb78d7b3824ca359512c023042a5 # git rev-parse v${pkgver}
source=("git+https://github.com/go-gitea/gitea.git#tag=${_tag}?signed"
        gitea.tmpfiles
        gitea.service
        gitea.sysusers
        gitea-arch-defaults.patch)
sha256sums=('SKIP'
            '1521fd7edc3830c695698ffe9835709f1408040b5ec989f07410972c894fa8ba'
            '7789b3f6699b9e111fa080226047cdc765e55ff49a3f72aac989c11c06c3e7e0'
            '7e7b798b8ce035c1fb55993ece41c5efb6cad5922708866804fa50ada0cf9fa5'
            '1fb97d8c77f5e66fcbe65b7e0c04e683adfb77ce89217cec16f2da998131b0f4')
validpgpkeys=(
  8C4033A23895237CB27D52D9D9B5613BEB813F99  # Matti Ranta <matti@mdranta.net> old RSA2048, retrieved from https://github.com/techknowlogick.gpg
  B56E3C7437A49E136862F5DE9D8A57ADAA232E95  # Matti Ranta <matti@mdranta.net> new RSA4096, retrieved from https://github.com/techknowlogick.gpg
  ED810FD31FBE67F406ED71BDD4F1E9B6493ED946  # Jonas Franz <info@jonasfranz.software>
  9C5BCD799B3CDB124147A748E0DDFEC24C48784C  # Lauris Bukšis-Haberkorns <lauris@nix.lv>
  D8F9672D77C0BB60A024C23EDFDE60A0093EB926  # Lauris Bukšis-Haberkorns <lauris@nix.lv> new RSA4096
  BA66F67FD73F7058D712D308C3B7C91B632F738A  # Lunny Xiao <xiaolunwen@gmail.com>, retrieved from https://github.com/lunny.gpg
) # Lunny Xiao <xiaolunwen@gmail.com>, retrieved from https://github.com/lunny.gpg

prepare() {
  cd ${pkgname}
  # Change some defaults for ArchLinux
  patch -Np1 -i ../gitea-arch-defaults.patch
  # Fetch dependency using go mod
  make vendor
}

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/^v//'
}

build() {
  cd ${pkgname}
  make generate
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\" -X \"code.gitea.io/gitea/modules/setting.AppWorkPath=/var/lib/gitea/\""
  make EXTRA_GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw" TAGS="sqlite pam" build
}

package() {
  install -Dm755 ${pkgname}/${pkgname} -t "${pkgdir}"/usr/bin/
  install -dm755 "${pkgdir}"/usr/share/${pkgname}
  cp -dr --no-preserve=ownership ${pkgname}/{options,public,templates} "${pkgdir}"/usr/share/${pkgname}/
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
  install -D ${pkgname}/custom/conf/app.ini.sample "${pkgdir}"/etc/gitea/app.ini
}
